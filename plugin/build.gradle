plugins {
    id 'java'
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

version = project.findProperty('projectVersion')

dependencies {
    implementation('org.bstats:bstats-bukkit:3.0.2')
    implementation('dev.dejvokep:boosted-yaml:1.3.5')
    implementation('dev.dejvokep:boosted-yaml-spigot:1.5')
    implementation('me.fixeddev:commandflow-universal:0.6.0')
    implementation('me.fixeddev:commandflow-bukkit:0.6.0')
    implementation('dev.triumphteam:triumph-gui:3.1.11')

    compileOnly('com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT')
    compileOnly('com.sk89q.worldguard:worldguard-core:7.1.0-SNAPSHOT')
    compileOnly("com.github.retrooper:packetevents-spigot:2.7.0")

    compileOnly 'net.citizensnpcs:citizensapi:2.0.35-SNAPSHOT'
    compileOnly(name:'ZNPCsPlus-2.0.0', ext:'jar')
    //compileOnly "lol.pyr:znpcsplus-api:2.0.0-SNAPSHOT"
    compileOnly("de.oliver:FancyNpcs:2.4.1")

    implementation "net.kyori:adventure-api:4.18.0"
    implementation 'net.kyori:adventure-platform-bukkit:4.1.0'
    implementation "net.kyori:adventure-text-minimessage:4.18.0"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set("character-dialogue")

    relocate 'me.fixeddev.commandflow', "me.iatog.characterdialogue.lib.commandflow"
    relocate 'com.google.gson', "me.iatog.characterdialogue.lib.gson"
    relocate 'dev.dejvokep.boostedyaml', 'me.iatog.characterdialogue.lib.boostedyaml'
    relocate 'net.kyori', 'me.iatog.characterdialogue.lib.kyori'
    relocate 'org.intellij', 'me.iatog.characterdialogue.lib.intellij'
    relocate 'org.jetbrains', 'me.iatog.characterdialogue.lib.jetbrains'
    relocate 'org.bstats', 'me.iatog.characterdialogue.lib.bstats'
    relocate 'dev.triumphteam.gui', 'me.iatog.characterdialogue.lib.triumphgui'
}

tasks.register('copyJars', Copy) {
    from tasks.named('shadowJar')
    into "$rootDir/build"
}

tasks.named('shadowJar').configure {
    finalizedBy copyJars
}

bukkit {
    name = 'CharacterDialogue'
    main = 'me.iatog.characterdialogue.CharacterDialoguePlugin'
    apiVersion = '1.20'
    version = project.findProperty('projectVersion')
    author = 'iAtog'
    softDepend = [
            'PlaceHolderAPI',
            'HolographicDisplays',
            'DecentHolograms',
            'floodgate',
            'ConditionalEvents',
            'Citizens',
            'ZNPCsPlus',
            'FancyNpcs',
            'WorldGuard',
            'packetevents'
    ]
    load = 'POSTWORLD'
    commands {

    }
}